goal: disjunction*

disjunction: disjunction "or" conjunction                                   -> or
    | conjunction                                                           -> conjunction

conjunction: conjunction "and" comparison                                   -> and
    | comparison                                                            -> comparison

comparison: sum "<=" sum                                                    -> less_equals
    | sum "<" sum                                                           -> less
    | sum ">=" sum                                                          -> greater_equals
    | sum ">" sum                                                           -> greater
    | sum "!=" sum                                                          -> not_equals
    | sum "==" sum                                                          -> equals
    | "(" disjunction ")"                                                   -> grouping
    | "true"                                                                -> true
    | "false"                                                               -> false

sum: sum "+" term                                                           -> add
    | sum "-" term                                                          -> minus
    | term                                                                  -> term

term: term "*" factor                                                       -> mult
    | term "/" factor                                                       -> div
    | factor                                                                -> factor

factor: "-" factor                                                          -> neg
    | primary                                                               -> primary

primary: NUMBER                                                             -> number
    | WORD                                                                  -> variable
    | WORD "[" sum "]"                                                      -> array_indexing
    | "["NUMBER ( "," NUMBER)*"]"                                           -> array_indexing_multi
    | "(" sum ")"                                                           -> grouping

%import common.WORD
%import common.NUMBER
%import common.WS
%ignore WS